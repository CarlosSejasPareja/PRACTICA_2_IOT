#include <WiFi.h>
#include "DHTesp.h"
#include <ArduinoJson.h>
#define DHTpin 26
#define TRIG_PIN 5
#define ECHO_PIN 18
DHTesp dht;
const char* WIFI_SSID = "pruebaIOT";
const char* WIFI_PASS = "contra123";
const char* SERVER_ADDRESS = "192.168.123.247";
const int SERVER_PORT = 1000;

void setup() {
  dht.setup(DHTpin, DHTesp::DHT11);
  pinMode(LED_BUILTIN, OUTPUT);
  Serial.begin(115200);
  Serial.print("Connecting to: ");
  Serial.println(WIFI_SSID);
  delay(1000);
  WiFi.begin(WIFI_SSID, WIFI_PASS);
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.println("Connecting to WiFi...");
  }
  Serial.println("Connected to WiFi");
  Serial.print("\nIP address: ");
  Serial.println(WiFi.localIP());
}

long readUltrasonicDistance(int triggerPin, int echoPin) {
  pinMode(triggerPin, OUTPUT);
  digitalWrite(triggerPin, LOW);
  delayMicroseconds(2);
  digitalWrite(triggerPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(triggerPin, LOW);
  pinMode(echoPin, INPUT);
  return pulseIn(echoPin, HIGH);
}
void sendDocumentToServer(long elapsed_time_in_microseconds, float temperature, float humidity, WiFiClient client)
{
  DynamicJsonDocument doc(256);
  doc["SET"] = 1;
  doc["time"] = elapsed_time_in_microseconds;
  doc["temperature"] = temperature;
  doc["humidity"] = humidity;
  String jsonString;
  serializeJson(doc, jsonString);
  client.print(jsonString);
}
void loop() {
  delay(100); 
  WiFiClient client;
  if (!client.connect(SERVER_ADDRESS, SERVER_PORT)) {
    Serial.println("Connection failed");
    return;
  }
  float humidity = dht.getHumidity();
  float temperature = dht.getTemperature();
  long elapsedMicroseconds = readUltrasonicDistance(TRIG_PIN, ECHO_PIN);
  sendDocumentToServer(elapsedMicroseconds, temperature, humidity, client);
}